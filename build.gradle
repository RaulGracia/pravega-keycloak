/**
 * Copyright (c) 2019 Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 */
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.jfrog.artifactory' version '4.10.0'
    id 'org.ajoberstar.grgit' version '3.1.1'
}

apply from: "$rootDir/gradle/version.gradle"

allprojects {
    apply plugin: 'jacoco'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'signing'

    repositories {
        jcenter()
    }

    group "io.pravega"
    sourceCompatibility = '1.8'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    dependencies {
        implementation platform("org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}")
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }
    check.dependsOn jacocoTestReport

//    signing {
//        sign publishing.publications
//    }

    publishing {
        publications {
            lib(MavenPublication) {
                pom {
                    url = 'http://pravega.io'
                    scm {
                        url = 'https://github.com/pravega/pravega-keycloak/tree/master'
                        connection = 'scm:git:git://github.com/pravega/pravega-keycloak.git'
                        developerConnection = 'scm:git:https://github.com/pravega/pravega-keycloak.git'
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }

    artifactory {
        contextUrl = publishUrl
        publish {
            repository {
                if (publishRepo?.trim()) {
                    repoKey = publishRepo
                }
                else if ("https://oss.jfrog.org" == "$publishUrl") {
                    repoKey = project.version.endsWith("-SNAPSHOT") ? "oss-snapshot-local" : "oss-release-local"
                }
                username = publishUsername
                password = publishPassword
            }
            defaults {
                publications('lib')
            }
        }
        resolve {
            repoKey = 'jcenter'
        }
        clientConfig.info.setBuildNumber(project.version)
    }

    bintray {
        user = publishUsername
        key = publishPassword
        pkg {
            repo = bintrayRepoName
            userOrg = bintrayOrgName
            licenses = ['Apache-2.0']
            publish = true
            vcsUrl = 'https://github.com/pravega/pravega-keycloak.git'
            version {
                gpg {
                    sign = true
                }
            }
        }
        publications = ['lib']
    }
}

project('client') {
    configurations {
        implementation {
            exclude group: 'org.projectlombok', module: 'lombok'
        }
        testImplementation {
            extendsFrom compileOnly
        }
    }

    dependencies {
        compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"
        compileOnly "io.pravega:pravega-shared-authplugin:${pravegaVersion}"
        compileOnly "io.pravega:pravega-client:${pravegaVersion}"
        implementation 'org.keycloak:keycloak-authz-client'

        testImplementation 'org.keycloak:keycloak-adapter-core'
        testImplementation "junit:junit:${junitVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    }

    task relocateShadowJar(type: ConfigureShadowRelocation) {
        target = tasks.shadowJar
        prefix = "io.pravega.keycloak"
    }
    shadowJar {
        mergeServiceFiles()
    }
    jar.enabled = false
    tasks.shadowJar.dependsOn tasks.relocateShadowJar
    tasks.build.dependsOn tasks.shadowJar

    publishing {
        publications {
            lib(MavenPublication) {
                artifactId = 'pravega-keycloak-client'
                artifact source: shadowJar, classifier: null
                artifact sourcesJar
                pom {
                    name = 'Pravega Keycloak Adapter (Client)'
                }
            }
        }
    }
    bintray.pkg.name = "io.pravega:pravega-keycloak-client"
}
