import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

buildscript {
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java-library'

    repositories {
        jcenter()
    }

    sourceCompatibility = '1.8'

    dependencies {
        implementation platform("org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}")
    }
}

project('client') {
    configurations {
        implementation {
            exclude group: 'org.projectlombok', module: 'lombok'
        }
    }

    dependencies {
        compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"
        compileOnly "io.pravega:pravega-shared-authplugin:${pravegaVersion}"
        compileOnly "io.pravega:pravega-client:${pravegaVersion}"
        implementation 'org.keycloak:keycloak-authz-client'
        implementation 'org.keycloak:keycloak-adapter-core'
        testImplementation "junit:junit:${junitVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    }

    shadowJar {
        mergeServiceFiles()
    }

    task relocateShadowJar(type: ConfigureShadowRelocation) {
        target = tasks.shadowJar
        prefix = "io.pravega.keycloak"
    }

    tasks.shadowJar.dependsOn tasks.relocateShadowJar
}
